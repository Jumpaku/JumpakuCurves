
buildscript {
    ext.jumpaku_curves_version = '0.11.6'
    ext.kotlin_version = '1.3.21'
    ext.jvm_version = '1.8'
    ext.dokka_version = '0.9.17'
    ext.dependencyVersions = [
            'junit4': '4.12',
            'junit5': '5.1.0',
            'hamcrest': '2.1',
            'commons_math3': '3.6.1',
            'kotson': '2.5.0',
            'vavr': '0.9.1',
            'fxgraphics2d': '1.8'
    ]

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

// Common configuration
subprojects {

    group 'jumpaku'
    version jumpaku_curves_version

    apply plugin: 'kotlin'
    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.dokka'

    repositories {
        mavenCentral()
        jcenter()
    }

    compileKotlin {
        kotlinOptions.jvmTarget = jvm_version
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = jvm_version
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

        compile "org.apache.commons:commons-math3:${dependencyVersions.commons_math3}"
        compile "com.github.salomonbrys.kotson:kotson:${dependencyVersions.kotson}"
        compile "io.vavr:vavr:${dependencyVersions.vavr}"
    }

    // Documentation
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    artifacts {
        archives sourcesJar
        archives javadocJar
    }
    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
    }

    // Publishing
    /**
     * To run uploadArchives,  ./gradlew uploadArchives -Pargs="`user id of nexus.jumpaku.net` `password`"
     */
    uploadArchives {
        def args = project.hasProperty("args") ? project.findProperty("args").split(/\s+/) : []
        def nexusUsername = args.size() >= 1 ? args[0] : "UNKNOWN USER"
        def nexusPassword = args.size() >= 2 ? args[1] : "NO PASSWORD"
        def nexusUrl = "https://nexus.jumpaku.net"
        repositories {
            mavenDeployer {
                def releaseUrl = "${nexusUrl}/repository/maven-releases/"
                repository(url: releaseUrl) {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }
                def snapshotUrl = "${nexusUrl}/repository/maven-snapshots/"
                snapshotRepository(url: snapshotUrl) {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }
            }
        }
    }
}

// Configurations for sub-projects
project(':core') {
}

project(':core-test') {
    dependencies {
        testCompileOnly "junit:junit:${dependencyVersions.junit4}"
        testImplementation "org.junit.jupiter:junit-jupiter-api:${dependencyVersions.junit5}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${dependencyVersions.junit5}"
        testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${dependencyVersions.junit5}"
        compile "org.hamcrest:hamcrest:${dependencyVersions.hamcrest}"

        compile project(':core')
    }
    test {
        useJUnitPlatform()
    }
}

project(':fsc') {
    dependencies {
        compile project(':core')
    }
}

project(':fsc-test') {
    dependencies {
        testCompileOnly "junit:junit:${dependencyVersions.junit4}"
        testImplementation "org.junit.jupiter:junit-jupiter-api:${dependencyVersions.junit5}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${dependencyVersions.junit5}"
        testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${dependencyVersions.junit5}"
        compile "org.hamcrest:hamcrest:${dependencyVersions.hamcrest}"

        compile project(':fsc')
        compile project(':core-test')
    }
    test {
        useJUnitPlatform()
    }
}

project(':graphics') {
    dependencies {
        compile project(':core')
        compile project(':fsc')
        compile "org.jfree:fxgraphics2d:${dependencyVersions.fxgraphics2d}"
    }
}

project(':demo'){
    dependencies {
        compile project(':core')
        compile project(':fsc')
        compile project(':graphics')
    }
}
